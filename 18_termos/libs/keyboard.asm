; ------------------------------------------------------------
; Процедура получения символа с клавиатуры
; На выходе будет AL (ascii)
; ------------------------------------------------------------

lkey    db 0                ; Последняя нажатая клавиша
kshift  db 0                ; SHIFT нажат (1) или отпущен (0)

getch:

        mov  ah, [lkey]
.prn:   in   al, 0x60       ; Последний статус с клавиши
        cmp  al, ah
        je  .prn

        ; ---------------------------------
        cmp  al, 0x2a       ; Нажатие SHIFT
        jne  .s1
        or  [kshift], 0x01  ; Отметить, что SHIFT нажат
.s1:    cmp  al, 0xAA
        jne  .s2    
        and [kshift], 0xfe  ; Отметить, что SHIFT отпущен        
.s2:    mov  ah, al         ; Объявить новый символ для блокировки
        ; ---------------------------------

        test al, 0x80       
        je  .kp             ; Данная клавиша была зажата (НЕ отпущена)? 
        mov  ah, 0          ; Если клавиша отпущена, включить готовность принять новый символ
.kp:    mov  [lkey], ah     ; Сохранить предыдущее значение

        cmp  al, 0x2a       ; SHIFT не транслировать
        je  .prn

        test al, 0x80
        jne .prn            ; Любую нажатую клавишу допустить

.press: 

        ; В зависимости от того, нажат или отпущен SHIFT, будет использоваться второй ряд клавиатуры
        mov bx, keycodes_lo
        test [kshift], byte 1
        je @f
        mov bx, keycodes_hi        
@@:     xlatb                     ; Трансляция клавиши в ее ASCII представление
        ret

        ;  1 NUM LOCK
        ;  8 BACKSPACE
        ;  9 TAB
        ; 27 ESCAPE

keycodes_lo:        

        ;  00  01   02  03   04   05   06   07   08   09   0a   0b   0c   0d   0e 0f 10   11   12   13   14   15   16   17   18   19   1a   1b   1c  1d 1e   1f   20   21   22   23   24   25    26  27   28    29   2a 2b    2c   2d   2e   2f   30   31   32   33   34   35   36 37   38 39   3a 3b 3c 3d 3e 3f 40 41 42 43 44 45 46 47 48 49 4a   4b 4c 4d 4e 
        db 0,  27, '1', '2', '3', '4', '5', '6', '7', '8', '9', '0', '-', '=', 8, 9, 'q', 'w', 'e', 'r', 't', 'y', 'u', 'i', 'o', 'p', '[', ']', 10, 0, 'a', 's', 'd', 'f', 'g', 'h', 'j', 'k', 'l', ';', 0x27, '`', 0, 0x5C, 'z', 'x', 'c', 'v', 'b', 'n', 'm', ',', '.', '/', 0, '*', 0, ' ', 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, '-', 0, 0, 0, '+'

keycodes_hi:        

        ;  00  01   02  03   04   05   06   07   08   09   0a   0b   0c   0d   0e 0f 10   11   12   13   14   15   16   17   18   19   1a   1b   1c  1d 1e   1f   20   21   22   23   24   25    26  27   28    29   2a 2b    2c   2d   2e   2f   30   31   32   33   34   35   36 37   38 39   3a 3b 3c 3d 3e 3f 40 41 42 43 44 45 46 47 48 49 4a   4b 4c 4d 4e 
        db 0, 255, '!', '@', '#', '$', '%', '^', '&', '*', '(', ')', '_', '+', 8, 9, 'Q', 'W', 'E', 'R', 'T', 'Y', 'U', 'I', 'O', 'P', '{', '}', 10, 0, 'A', 'S', 'D', 'F', 'G', 'H', 'J', 'K', 'L', ':', '"',  '~', 0,  '|', 'Z', 'X', 'C', 'V', 'B', 'N', 'M', '<', '>', '?', 0, '*', 0, ' ', 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 0, 0, '-', 0, 0, 0, '+'

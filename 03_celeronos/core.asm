; **********************************************************************
; ЧАСТЬ 3. Ядро
; ----------------------------------------------------------------------
; Загружается после boot-manager
; ----------------------------------------------------------------------
; Этот модуль предназначен для входа в защищенный режим процессора
; Сегменты защищенного режима назначаются простые и базовые. После, GDT
; будет постепенно расширяться и уменьшаться, в зависимости от 
; количества запущенных процессов
; **********************************************************************

    org     0x800
    
    macro   brk { xchg bx, bx }

    cli
    cld    
    
    ; В данный момент "четкий" режим 640 x 480 x 16
    mov     ax, 0x0012
    int     0x10

    ; Загрузим указатель GDT на те сегменты, что сверху прописаны (кода и данных)
    ; Здесь IDT менять не будем (не требуется для коротких операции)

    mov     word  [Descriptor_Table.gdt + 0], (4*8) - 1         ; Количество элементов в GDT
    mov     dword [Descriptor_Table.gdt + 2], Descriptor_Table  ; Начало GDT, линейный адрес
    lgdt          [Descriptor_Table.gdt]

    ; Установка нового указателя IDT
    mov     word  [Descriptor_Table.idt + 0], 0x7FF ; Количество элементов в IDT (256) 8*256-1 = 2047
    mov     dword [Descriptor_Table.idt + 2], 0     ; Начало IDT, линейный адрес
    lidt          [Descriptor_Table.idt]

    ; Установка особого бита PM=1
    mov     eax, cr0
    or      al,  1
    mov     cr0, eax

    ; Переход в PM
    jmp     0x08 : __start_protected_mode

; ----------------------------------------------------------------------
; Инициализация глобальных дескрипторов
; ----------------------------------------------------------------------

Descriptor_Table: 

; Пустой селектор (NULL)
.null:

    dd 0, 0

; 08h 4Гб, вся память, сегмент кода
.code:

    dw 0xffff              ; limit[15..0]
    dw 0                   ; addr[15..0]
    db 0                   ; addr[23..16]
    db 80h + (10h + 8)     ; тип=8 (код для чтения) + 10h (s=1) + 80h (p=1), dpl = 0
    db 80h + 0xF + 40h     ; limit[23..16]=0x0f, G=1, D=1
    db 0                   ; addr[31..24]

; 10h 4Гб, вся память, данные
.data:

    dw 0xffff
    dw 0
    db 0    
    db 80h + (10h + 2)     ; тип=2 (данные для чтения и записи) + 10h (s=1) + 80h (p=1), dpl = 0
    db 80h + 0xF + 40h     ; G=1, D=1, limit=0
    db 0

; 18h Дескриптор TSS
.tss: 

    dw 103                      ; Размер TSS (Обычно 104 байта) 104-1
    dw Main_Task_Segment_State  ; Ссылка на TSS
    db 0
    db 80h + 9                  ; 32-битный свободный TSS, P=1
    db 40h                      ; DPL=0, G=0, D=1 (32 битный)
    db 0 

.gdt:  dw 0,0,0                ; Указатель на GDT
.idt:  dw 0,0,0                ; Указатель на IDT

; ----------------------------------------------------------------------
; Основной TSS (Task Segment State)
; ----------------------------------------------------------------------

Main_Task_Segment_State: 

    dw 0, 0         ; 00 -- / LINK
    dd 0            ; 04      ESP0
    dw 0, 0         ; 08 -- / SS0
    dd 0            ; 0C      ESP1
    dw 0, 0         ; 10 -- / SS1
    dd 0            ; 14      ESP2
    dw 0, 0         ; 18 -- / SS2
    dd 0            ; 1C CR3
    dd 0            ; 20 EIP
    dd 0            ; 24 EFLAGS
    dd 0            ; 28 EAX
    dd 0            ; 2C ECX
    dd 0            ; 30 EDX 
    dd 0            ; 34 EBX
    dd 0            ; 38 ESP
    dd 0            ; 3C EBP
    dd 0            ; 40 ESI
    dd 0            ; 44 EDI
    dw 0, 0         ; 48 -- / ES
    dw 0, 0         ; 4C -- / CS
    dw 0, 0         ; 50 -- / SS
    dw 0, 0         ; 54 -- / DS
    dw 0, 0         ; 58 -- / FS
    dw 0, 0         ; 5C -- / GS
    dw 0, 0         ; 60 -- / LDTR
    dw 104, 0       ; 64 IOPB offset / --

; ----------------------------------------------------------------------
; ЧАСТЬ 4. Запуск ядра в Protected Mode
; ----------------------------------------------------------------------

__start_protected_mode:

    use32

    mov     ax, 0x10 ; CODE RING-0
    mov     ds, ax
    mov     es, ax
    mov     ss, ax
    mov     fs, ax
    mov     gs, ax
    mov     ax, 0x18 ; TSS
    ltr     ax
    
    ; ---------------------------------
    ; Распаковка RLE в 1`00000h
    ; ---------------------------------

    mov     esi, kernelcc
    mov     edi, 0x100000
    
.iter:

    lodsb
    
    ; Поток завершен, перейти к программе
    and     al, al
    je      0x100000
    test    al, 0x80
    je      .unpack
    
    and     al, 0x7F
    movzx   ecx, al
    lodsb
    rep     stosb
    jmp     .iter
    
.unpack:

    movzx   ecx, al
    rep     movsb
    jmp     .iter

; ЯДРО, написанно на православном языке С.
; ----------------------------------------------------------------------

; Скомпилированное упакованное ядро
kernelcc:   file "kernel.rle"

; --- а тут будет виртуальный диск
